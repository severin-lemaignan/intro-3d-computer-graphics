def O (0,0,0)

%%% cube
def p1 (1,0,0) def p2 (1,0,1) def p3 (0,0,1)
def p4 (0,1,0) def p5 (0,1,1) def p6 (1,1,1) def p7 (1,1,0)

% faces of the cube

def ctrans scale(0.6) then translate([0.5,0.5,0.5])

def cube {
    put {[[ctrans]]} {
        polygon[cull=false,fill style=finalbox](O)(p1)(p2)(p3) % bottom
        polygon[cull=false,fill style=finalbox](O)(p1)(p7)(p4) % front
        polygon[cull=false,fill style=finalbox](p1)(p7)(p6)(p2) % right
        polygon[cull=false,fill style=finalbox](O)(p3)(p5)(p4) % left
        polygon[cull=false,fill style=finalbox](p4)(p5)(p6)(p7) % top
        polygon[cull=false,fill style=finalbox](p5)(p3)(p2)(p6) % back
    }
}

def tp6 [[ctrans]] * (p6)

%%% camera
def p8 (2,2,2)
def p9 (-1,2,2)
def p10 (-1,-1,2)
def p11 (2,-1,2)

def mycamera {
    put {translate([-0.5,-0.5,-0.5]) then scale(0.5)} {
        polygon[cull=false,fill style=box](O)(p1)(p2)(p3) % bottom
        polygon[cull=false,fill style=box](O)(p1)(p7)(p4) % front
        polygon[cull=false,fill style=box](p1)(p7)(p6)(p2) % right
        polygon[cull=false,fill style=box](O)(p3)(p5)(p4) % left
        polygon[cull=false,fill style=box](p4)(p5)(p6)(p7) % top

        polygon[cull=false,fill style=box](p6)(p8)(p9)(p5) 
        polygon[cull=false,fill style=box](p6)(p2)(p11)(p8) 
        polygon[cull=false,fill style=box](p2)(p3)(p10)(p11)
        polygon[cull=false,fill style=box](p5)(p9)(p10)(p3) 

        polygon[cull=false,fill style=box](p8)(p9)(p10)(p11) % back
    }
}


def axes {
    def sz 1
    line [line style=axis, color=green,arrows=->] (O)(sz,0,0)
    line [line style=axis, color=red,arrows=->] (O)(0,sz,0)
    line [line style=axis, color=blue,arrows=->] (O)(0,0,sz)
    special |\node at #1 {\tiny x};\node at #2 {\tiny y};\node at #3 {\tiny z};|[lay=in]
    (sz * 1.2,0,0)(0,sz * 1.2,0)(0,0,sz * 1.2)
}

def t1 rotate(-40, [0,1,0]) then translate([1,2,-2])
def t2 rotate(120, [0,1,0]) then rotate(30,[1,0,0])  then translate([-5,1,-5])

def a1 [[t1]]*(O)
def a2 [[t2]]*(O)

def axes0 {
            {axes} 
            {special |\node at #1 {\small Monde};| (0.5,-0.5,0)}
}

def object {
    put {[[t1]]} {
            {axes} 
            {special |\node at #1 {\small Objet};| (0.5,-0.5,0)}
            {cube}
            {dots[dotsize=3pt,fill=yellow,opacity=0.8] (tp6)}
    }
}

def cameraframe {
    put {[[t2]]} {
            {axes} 
            {special |\node at #1 {\small Camera};| (0,-0.5,-1)}
            {mycamera} 
    }
}


def eye (5,4,10)
def look_at (0,0,-5)


put { view((eye), (look_at)) then perspective(12) } {
    {axes0} 
    {object} 
    line[style=dashed] (O) (a1)
    {cameraframe}
    line[style=dashed] (O) (a2)
}

global { language tikz }



